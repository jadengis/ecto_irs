defmodule EctoIRS.Schema do
  @moduledoc """
  Ecto schema extensions for auditing capabilities.
  """

  @doc false
  defmacro __using__(_) do
    quote do
      import EctoIRS.Schema

      @audits_opts []
    end
  end

  @doc """
  Generates `:inserted_by` and `:updated_by` audit fields.

  The fields generated by this macro will automatically be set to
  the subject from the current context.

  ## Options
    * `:inserted_by` - the Ecto schema name of the field for insertion times or `false`
    * `:updated_by` - the Ecto schema name of the field for update times or `false`
    * `:references` - the field on `type` the audits reference.
    * `:autogenerate` - a module-function-args tuple used for generating both `inserted_by_id`
      and `updated_by_id`.

  All options can be pre-configured by setting `@audits_opts`.
  """
  defmacro audits(type, opts \\ []) do
    quote bind_quoted: binding() do
      audits = Keyword.merge(@audits_opts, opts)

      autogen = audits[:autogenerate]

      inserted_by = Keyword.get(audits, :inserted_by, :inserted_by)
      updated_by = Keyword.get(audits, :updated_by, :updated_by)
      opts = if references = audits[:references], do: [references: references], else: []

      if inserted_by do
        Ecto.Schema.belongs_to(inserted_by, type, opts)
      end

      if updated_by do
        Ecto.Schema.belongs_to(updated_by, type, opts)

        with {m, f, a} when is_atom(m) and is_atom(f) and is_list(a) <- autogen do
          Module.put_attribute(__MODULE__, :ecto_autoupdate, {[:"#{updated_by}_id"], autogen})
        end
      end

      with {m, f, a} when is_atom(m) and is_atom(f) and is_list(a) <- autogen,
           [_ | _] = fields <- Enum.filter([inserted_by, updated_by], & &1) do
        Module.put_attribute(
          __MODULE__,
          :ecto_autogenerate,
          {Enum.map(fields, &:"#{&1}_id"), autogen}
        )
      end

      :ok
    end
  end
end
